"Tilt functions for building containers with Bazel."

load("ext://restart_process", "custom_build_with_restart")
load("../../core.tilt", "watch_files")
load("../../docker.tilt", "image_entrypoint", "image_inspect")
load("./docker.tilt", "bazel_image")
load("./files.tilt", "bazel_build_files", "bazel_source_files")

def _build_image_cmd(target):
    cmd = ("bazel run {target} -- --norun " +
           "&& docker tag {bazel_image} $EXPECTED_REF")
    return cmd.format(bazel_image = bazel_image(target), target = target)

def bazel_build(ref, target):
    "Builds a Bazel target image."
    watch_files(bazel_build_files(target))
    custom_build(
        command = _build_image_cmd(target),
        ref = ref,
        deps = bazel_source_files(target),
    )

def bazel_build_with_restart(ref, target, live_update, deps = []):
    "Builds a Bazel target image with live updates."
    image_info = image_inspect(bazel_image(target))
    watch_files(bazel_build_files(target))
    custom_build_with_restart(
        command = _build_image_cmd(target),
        entrypoint = image_entrypoint(image_info),
        live_update = live_update,
        ref = ref,
        deps = bazel_source_files(target) + deps,
    )
